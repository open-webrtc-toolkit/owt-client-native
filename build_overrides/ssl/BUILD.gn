# Copyright (C) <2018> Intel Corporation
#
# SPDX-License-Identifier: Apache-2.0

import("ssl.gni")

if (owt_use_openssl) {
  config("external_openssl") {
    assert(
        owt_openssl_header_root != "",
        "You must specify owt_openssl_header_root when owt_use_openssl==true.")
    assert(
        owt_openssl_lib_root != "",
        "You must specify owt_openssl_lib_root when owt_use_openssl==true.")
    include_dirs = [ owt_openssl_header_root ]
    lib_dirs = [ owt_openssl_lib_root ]
    if (is_ios) {
      libs = [
        "crypto",
        "ssl",
      ]
    } else if (is_win) {
      libs = [
        "libcrypto.lib",
        "libssl.lib",
      ]

      if (is_clang) {
        defines = [ "__GNUC__" ]
      }
    } else {
      libs = [
        "libeay32.lib",
        "ssleay32.lib",
      ]
    }
    if (is_clang) {
      cflags = [ "-Wno-deprecated-declarations" ]
    }
  }
}
